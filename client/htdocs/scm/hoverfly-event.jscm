;; -*- mode: scheme; -*-
;; Why We Love Wasps Copyright (C) 2019 FoAM Kernow
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU Affero General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU Affero General Public License for more details.
;;
;; You should have received a copy of the GNU Affero General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(define (hoverfly-reproduce nest)
  (foldl ;; add on new hoverflies
   (lambda (hoverfly r)
     (if (eq? (hoverfly-state hoverfly) 'state-lay)
	 (append
	  (build-list
	   9
	   (lambda (_)
	     (make-hoverfly-grub
	      (v2add (vec2 (* (crndf) 20) (* (crndf) 20))
		     (entity-pos hoverfly))
	      (hoverfly-dest-egg hoverfly)))) r)
	 r))
   (event-data (nest-event nest))
   (event-data (nest-event nest))))

(define (hoverfly-kill-wasps nest)
  (foldl
   (lambda (hoverfly n)
     (if (hoverfly-killed-egg hoverfly)
	 (nest-modify-wasps
	  n (entity-list-modify
	     (nest-wasps n)
	     (hoverfly-dest-egg hoverfly)
	     (lambda (e)
	       (wasp-modify-state e 'state-eaten-egg))))
	 n))
   nest
   (event-data (nest-event nest))))

(define (hoverfly-attack-builder)
  (lambda ()
    (make-event 
     'hoverfly (local-get-param "wasp-game" "hoverfly-attack-duration")
     (build-list
      (local-get-param "wasp-game" "hoverfly-attack-number")
      (lambda (i) (make-hoverfly)))
     (lambda (nest delta state)
       (let ((event (nest-event nest)))
	 ;; event will be false for the last tick!...
	 (if event
	     (nest-modify-event
	      (hoverfly-kill-wasps nest)
	      (event-modify-data event (hoverfly-reproduce nest)))       
	     nest)))
     (lambda (nest)
       #t))))

(define (hoverfly-attack-update event nest delta)
  (event-modify-data
   event
   (map
    (lambda (hoverfly)
      (entity-update (hoverfly-update hoverfly delta nest) delta))
    (event-data event))))

(define (hoverfly-attack-render-entities renderer event)
  (renderer-add-entities renderer (event-data event)))

(define (hoverfly-attack-render ctx event)
  (info-box-render-box ctx)
  (info-box-render-text ctx "HOVERFLIES ARE SEARCHING FOR EGGS TO PARASITISE!"))



