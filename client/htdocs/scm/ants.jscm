;; -*- mode: scheme; -*-
;; Why We Love Wasps Copyright (C) 2019 FoAM Kernow
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU Affero General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU Affero General Public License for more details.
;;
;; You should have received a copy of the GNU Affero General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(define ant-speed (local-get-param "wasp-game" "ant-speed"))

(define ant-animation
  (list
   (make-anim-move "ant" "ant" (list "1" "2") 15)
   ))

(define ant-animation-walk 0)

(define (make-ant) 
  (let ((pos (vec2 2000 1000)))
    (append 
     (make-entity 
      (generate-entity-id!)
      "ant" 
      pos
      (vec2 300 300)
      "left"
      (rndf)
      (anim-load-frames ant-animation)
      0 ant-animation-walk #f 0 pos pos 0 0)
     (list 'state-appear 0))))

(define (ant-state a) (list-ref a (+ entity-size 0)))
(define (ant-modify-state a s) (list-replace a (+ entity-size 0) s))
(define (ant-dest-cell w) (list-ref w (+ entity-size 1)))
(define (ant-modify-dest-cell w s) (list-replace w (+ entity-size 1) s))

(define (ant-walk-to-cell a dest-cell)
  (entity-walk-to-pos
   (ant-modify-dest-cell a dest-cell)
   (cell-pos dest-cell)))

(define (ant-update a delta nest)
  (ant-update-animation
   (let ((state (ant-state a)))
     (cond
      
      ((eq? state 'state-appear)
       ;; random timer
       (if (< (rndf) 0.01)
	   (ant-modify-state a 'state-search)
	   a))
      
      ((eq? state 'state-search)
       (let ((pupa (nest-find-pupa nest)))
	 ;; head to a pupa or a random position
	 (ant-modify-state
	  (entity-walk-to-pos a (if pupa
				    (entity-pos pupa)
				    (vec2 (* (rndf) 1000)
					  (* (rndf) 500))))
	  'state-wander)))
      
      ((eq? state 'state-wander)
       (if (>= (entity-walk-time a) 1)
	   (ant-modify-state a 'state-search)
	   (entity-walk a ant-speed delta)))
      
      (else a)))
   delta))

(define (ant-update-animation w delta)  
  (let ((state (ant-state w)))
    (entity-modify-clip 
     (entity-modify-timer w (+ (entity-timer w) delta))
     (cond
      ((eq? state 'state-appear) ant-animation-walk)
      ((eq? state 'state-wander) ant-animation-walk)
      ((eq? state 'state-search) ant-animation-walk)
      (else (entity-clip w))))))
